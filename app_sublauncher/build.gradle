plugins {
    id 'com.android.application' version '8.7.2'
}

static def getDate() { return new Date().format('yyyyMMdd') }

static def getDateSeconds() {
    if (System.getenv("GITHUB_ACTIONS") == "true") {
        return 9934841 + Integer.parseInt(System.getenv("GITHUB_RUN_NUMBER"))
    } else {
        return 172005
    }
}

def getVersionName = {
    ByteArrayOutputStream TAG = new ByteArrayOutputStream()
    ByteArrayOutputStream BRANCH = new ByteArrayOutputStream()
    ByteArrayOutputStream TAG_PART_COMMIT = new ByteArrayOutputStream()
    String TAG_STRING

    exec {
        try {
            commandLine 'git', 'describe', '--tags'
            ignoreExitValue true
            standardOutput = TAG
        } catch (Exception e) {
            e.printStackTrace();
        }
    }

    if (TAG.toString() == "") {
        exec {
            try {
                commandLine 'git', 'describe', '--always', '--tags'
                ignoreExitValue true
                standardOutput = TAG_PART_COMMIT
            } catch (Exception e) {
            }
        }
    }

    if(TAG.toString() != ""){
        TAG_STRING = TAG.toString()
    }else {
        if(TAG_PART_COMMIT.toString().trim() == ""){
            return ("LOCAL-" + "${getDate()}")
        }else {
            TAG_STRING =  'sublauncher-' + "${getDate()}" + "-" + TAG_PART_COMMIT.toString().trim()
        }
    }

    exec {
        try {
            commandLine 'git', 'branch', '--show-current'
            ignoreExitValue true
            standardOutput = BRANCH
        } catch (Exception e) {
            e.printStackTrace();
        }
    }

    return TAG_STRING.trim().replace("-g", "-") + "-" + BRANCH.toString().trim()
}

android {
    namespace 'com.subhobhai.sublauncher'

    compileSdk = 34

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        customDebug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    defaultConfig {
        applicationId "com.subhobhai.sublauncher"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode getDateSeconds()
        versionName getVersionName()
        multiDexEnabled true
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.customDebug
            resValue 'string', 'application_package', 'com.subhobhai.sublauncher.debug'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue 'string', 'application_package', 'com.subhobhai.sublauncher'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        buildConfig true
    }

    buildToolsVersion = '34.0.0'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.annotation:annotation:1.7.1'
    
    // Core dependencies for launcher functionality
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'org.tukaani:xz:1.9'
}